buildscript {
    repositories {
        jcenter()
        mavenCentral()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
        maven {
            url = "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.2-SNAPSHOT'
        classpath "gradle.plugin.com.matthewprenger:CurseGradle:1.0.9"
    }
}

apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'com.matthewprenger.cursegradle'

project.ext {
    buildNum = System.getenv("CIRCLE_BUILD_NUM")
    mcVersion = "1.11.2"
    curseApiKey = System.getenv("CURSE_API_KEY") ?: ""
    curseRelease = System.getenv("CIRCLE_RELEASE_TYPE") ?: "alpha"
}

configurations {
    shade
    compileOnly.extendsFrom shade
}

def getLastCommit = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'log', '-1', '--pretty=%B'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

version = project.ext.mcVersion + "-0.1." + project.ext.buildNum
group = "ca.glasspelican.worldhistory"
archivesBaseName = "worldHistory"

dependencies {
    shade "mysql:mysql-connector-java:6.0.6"
    shade 'com.h2database:h2:1.4.196'
    testCompile 'junit:junit:4.12'
}

sourceCompatibility = targetCompatibility = "1.8"
compileJava {
    sourceCompatibility = targetCompatibility = "1.8"
}

minecraft {
    version = "${project.ext.mcVersion}-13.20.1.2429"
    replace "@VERSION@", project.version
    replace "@BUILDNUM@", project.ext.buildNum

    runDir = "run"
    mappings = "snapshot_20161220"
}

curseforge {
    apiKey = project.ext.curseApiKey
    project {
        id = '265169'
        releaseType = project.ext.curseRelease
        changelog = getLastCommit()
        mainArtifact jar
        addArtifact sourceJar
    }
}

jar {
    configurations.shade.each { dep ->
        from(project.zipTree(dep)){
            exclude 'META-INF', 'META-INF/**'
        }
    }
}

processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        //include 'mcmod.info'

        // replace version and mcversion
        expand 'version': project.version, 'mcversion': project.minecraft.version
    }

    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}